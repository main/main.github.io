<!DOCTYPE html>
<html lang="en" >
<head>
  <meta charset="UTF-8">
  <title>Awake Holism</title>
  <link rel="stylesheet" href="./style.css">

</head>
<body>
<div id="shadercollab"></div>
<script id="vertex" type="x-shader/x-vertex">
	void main() { gl_Position = vec4(position, 1.0); }
</script>

<script id="fragment" type="x-shader/x-vertex">
precision highp float;
	
#define M_PI 3.1415926535897932384626433832795

uniform vec2 u_resolution;
uniform float u_time;

vec4 permute(vec4 x){return mod(((x*34.0)+1.0)*x, 289.0);}
vec2 fade(vec2 t) {return t*t*t*(t*(t*6.0-15.0)+10.0);}

float cnoise(vec2 P){
  vec4 Pi = floor(P.xyxy) + vec4(0.0, 0.0, 1.0, 1.0);
  vec4 Pf = fract(P.xyxy) - vec4(0.0, 0.0, 1.0, 1.0);
  Pi = mod(Pi, 289.0); // To avoid truncation effects in permutation
  vec4 ix = Pi.xzxz;
  vec4 iy = Pi.yyww;
  vec4 fx = Pf.xzxz;
  vec4 fy = Pf.yyww;
  vec4 i = permute(permute(ix) + iy);
  vec4 gx = 2.0 * fract(i * 0.0243902439) - 1.0; // 1/41 = 0.024...
  vec4 gy = abs(gx) - 0.5;
  vec4 tx = floor(gx + 0.5);
  gx = gx - tx;
  vec2 g00 = vec2(gx.x,gy.x);
  vec2 g10 = vec2(gx.y,gy.y);
  vec2 g01 = vec2(gx.z,gy.z);
  vec2 g11 = vec2(gx.w,gy.w);
  vec4 norm = 1.79284291400159 - 0.85373472095314 * 
    vec4(dot(g00, g00), dot(g01, g01), dot(g10, g10), dot(g11, g11));
  g00 *= norm.x;
  g01 *= norm.y;
  g10 *= norm.z;
  g11 *= norm.w;
  float n00 = dot(g00, vec2(fx.x, fy.x));
  float n10 = dot(g10, vec2(fx.y, fy.y));
  float n01 = dot(g01, vec2(fx.z, fy.z));
  float n11 = dot(g11, vec2(fx.w, fy.w));
  vec2 fade_xy = fade(Pf.xy);
  vec2 n_x = mix(vec2(n00, n01), vec2(n10, n11), fade_xy.x);
  float n_xy = mix(n_x.x, n_x.y, fade_xy.y);
  return 2.3 * n_xy;
}
	
// Transform UV to radial UV
vec2 getRadialUv(vec2 uv)
{
	float angle = atan(uv.x, - uv.y);
	angle = abs(angle);
	
	vec2 radialUv = vec2(0.0);
	radialUv.x = angle / (M_PI * 2.0) + 0.5;
	radialUv.y = 1.0 - pow(1.0 - length(uv), 4.0);
	
	return radialUv;
}
	
// Get the elevation using perlin noises
float getElevation(vec2 uv)
{
	float elevation = cnoise(uv * 15.0);
	elevation += cnoise(uv * 80.0) * 0.2;
	elevation += cnoise(uv * 200.0) * 0.1;
	if(elevation > - 0.15)
		elevation += abs(cnoise(uv * 900.0) * 0.15);
	
	return elevation;
}

// Get terrain color
vec3 getTerrainColor(float elevation)
{
	// Set colors
	vec3 sandColor = vec3(0.96, 0.64, 0.38); // sandybrown
	vec3 forestColor = vec3(0.0, 0.4, 0.1); // green
	vec3 meltedSnowColor = vec3(0.7, 0.9, 1.0); // melted snow
	vec3 snowColor = vec3(1.0); // snow
	
	// Get sand and forest mix
	vec3 color = mix(sandColor, forestColor, elevation);
	
	if(elevation > 0.7)
	{
		color = mix(meltedSnowColor, snowColor, (elevation - 0.7) / 0.3);
	}
	
	return color;
}
	
// Get water color
vec3 getWaterColor(float elevation)
{
	// Set colors
	vec3 depthColor = vec3(0.2, 0.54, 0.75) * 0.2;
	vec3 shoreColor = vec3(0.2, 0.54, 0.75) * 0.8;
	
	// Mix colors
	float strength = - elevation;
	vec3 color = mix(shoreColor, depthColor, strength);
	
	return color;
}
	
// Get shadow
float getShadow(float elevation, vec2 uv, vec2 animatedUv)
{
	vec2 towardSun = normalize(- uv);
	
	const float iterations = 4.0;
	float shadow = 0.0;
	
	for(float i = 1.0; i <= iterations; i++)
	{
		float towardElevation = getElevation(animatedUv + towardSun / iterations * 0.01 * i);
		if(towardElevation > elevation)
		{
			shadow += 1.0 / iterations;
		}
	}
	
	return shadow;
}

// Get sun color and intensity
vec4 getSun(vec2 radialUv)
{
	vec3 innerColor = vec3(1.0); // white
	vec3 outerColor = vec3(0.9, 0.8, 0.5); // yellow
	
	float strength = 1.0 - radialUv.y * 1.4 + 0.4;
	strength += clamp(cnoise(vec2(radialUv.x * 10.0, u_time * 0.2)), 0.0, 1.0) * 0.6;
	strength = clamp(strength, 0.0, 1.0);
	vec3 color = mix(innerColor, outerColor, radialUv.y * 1.0);
	
	return vec4(color, strength);
}
	
void main()
{
	vec2 uv = (gl_FragCoord.xy - u_resolution * .5) / u_resolution.xx + 0.5;
	uv -= 0.5;
	
	vec2 radialUv = getRadialUv(uv);
	
	vec2 animatedRadialUv = radialUv;
	animatedRadialUv.y -= u_time * 0.01;
	animatedRadialUv.x -= u_time * 0.002;
	
	float elevation = getElevation(animatedRadialUv);
	
	vec3 color = vec3(0.0);
	
	float shadow = getShadow(elevation, radialUv, animatedRadialUv);
	if(elevation > 0.0)
	{
		color = getTerrainColor(elevation);
		color -= shadow * 0.5;
	}
	else
	{
		color = getWaterColor(elevation);
		color -= shadow * 0.1;
	}
	
	vec4 sunColor = getSun(radialUv);
	color = mix(color, sunColor.rgb, sunColor.a);

    gl_FragColor = vec4(color, 1.0);
}
</script>
<!-- partial -->
  <script src='https://cdnjs.cloudflare.com/ajax/libs/three.js/r128/three.min.js'></script><script  src="./script.js"></script>

</body>
</html>
